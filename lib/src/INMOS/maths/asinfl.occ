#OPTION "V"  -- no vector space
#COMMENT "Floating point maths library, V1.1 27/03/90"
#COMMENT "(c) Copyright INMOS Limited 1988"
#INCLUDE "mathhdr.inc"
#ifdef C
#PRAGMA TRANSLATE ASIN "ASIN%c"
#else
#ifdef FORTRAN
#PRAGMA TRANSLATE ASIN "ASIN%f"
#PRAGMA TRANSLATE ASINP "ASINP%f"
#endif
#endif

REAL32 FUNCTION ASIN (VAL REAL32 X)

  --{{{  history
  -- V1.0 1-Jul-88
  -- Added OPTION "V"; V1.1 27-Mar-90 SRH
  --}}}

  REAL32 Result :
  VALOF
    IF
      NOTFINITE (X)
        --{{{  NaNs and Infs
        IF
          ISNAN (X)
            Result := X
          TRUE
            Result := undefined.NaN
        --}}}
      --{{{  definitions
      VAL P2 IS -5.04400557E-1(REAL32) :
      VAL P1 IS  9.33935835E-1(REAL32) :
      VAL Q1 IS -5.54846723(REAL32) :
      VAL Q0 IS  5.60363004(REAL32) :
      VAL XWork IS ABS (X) :
      --}}}
      IF
        XWork > One
          Result := undefined.NaN
        XWork = One
          --{{{  result depends on sign
          IF
            X < Zero
              Result := - PiBy2
            TRUE
              Result := PiBy2
          --}}}
        XWork > Half
          --{{{  reduce argument & compute result
          REAL32 Y, G, RWork :
          SEQ
            G := DIVBY2 (One - XWork)
            Y := - MULBY2 (SQRT (G))
            RWork := Y + (Y * ((((P2 * G) + P1) * G)/(((G + Q1) * G) + Q0)))
            IF
              X < Zero
                Result := - (RWork + PiBy2)
              TRUE
                Result := RWork + PiBy2
          --}}}
        TRUE
          --{{{  remaining case
          VAL G IS X * X :
          Result := X + (X * ((((P2 * G) + P1) * G)/(((G + Q1) * G) + Q0)))
          --}}}
    RESULT Result
:
#ifdef FORTRAN
--{{{  ASINP
REAL32 FUNCTION ASINP( VAL INT dummy, VAL INT X )

  -- ASINP exists for when the FORTRAN intrinsic ASIN is passed as a
  -- parameter to a routine.  The FORTRAN compiler will see the interface
  -- to ASINP.  When ASINP is called from FORTRAN the default FORTRAN
  -- calling conventions are used, i.e. pass a gsb pointer and pass
  -- parameters by reference.  Due to these facts ASINP has a dummy
  -- parameter to catch the gsb and ASM code to dereference its argument
  -- which is in fact a pointer to the true argument.
  -- We use INTs as arguments for ASINP because an INT has the same size
  -- as a pointer.

  REAL32 Xtemp :
  VALOF
    ASM
      LD       X
      LDNL     0
      ST       Xtemp
    RESULT ASIN( Xtemp )
:
--}}}
#endif
