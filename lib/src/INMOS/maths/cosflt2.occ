#OPTION "V"  -- no vector space
#COMMENT "Floating point maths library, V1.1 27/03/90"
#COMMENT "(c) Copyright INMOS Limited 1988"
#INCLUDE "mathhdr.inc"
#ifdef C
#PRAGMA TRANSLATE COS "COS%c"
#endif

REAL32 FUNCTION COS (VAL REAL32 X)

  --{{{  history
  -- V1.0 1-Jul-88
  -- Added OPTION "V"; V1.1 27-Mar-90 SRH
  --}}}

  REAL32 RWork :
  VALOF
    IF
      NOTFINITE (X)
        --{{{  Inf or NaN
        IF
          ISNAN (X)
            RWork := X    -- transmit NaN
          TRUE
            RWork := undefined.NaN
        --}}}
      TRUE
        --{{{  definitions and variables
        -- Limit on X is Pi * 2**(ManBits/2). See C&W p134.
        VAL REAL32 XMax RETYPES   #46490FDB(INT32) :
        -- C1 + C2 = Pi to extra precision
        VAL REAL32 C1    RETYPES  #40490000(INT32) :
        VAL REAL32 C2    RETYPES  #3A7DAA22(INT32) :
        REAL32 XWork :
        --}}}
        SEQ
          XWork := ABS (X)
          IF
            --{{{  special case
            XWork > XMax
              RWork := inexact.NaN
            -- Threshold for sin(x) = x is 2 ** (-ManBits/2), see C&W pp137.
            -- This is consequently also a threshold for cos(x) = 1.
            --}}}
            TRUE
              INT N :
              REAL32 XN :
              SEQ
                --{{{  reduce argument
                N := INT ROUND ((XWork + PiBy2) * InvPi)
                XN := DIVBY2 (REAL32 ROUND ((N << 1) - 1))
                XWork := (XWork - (XN * C1)) - (XN * C2)
                --}}}
                --{{{  approximation in primary range
                IF
                  ABS(XWork) > Nearly.PiBy2
                    IF
                      XWork < Zero
                        RWork := - One
                      TRUE
                        RWork := One
                  TRUE
                    -- XWork is in the range [-Nearly.PiBy2, Nearly.PiBy2]
                    -- This implementation uses a polynomial approximation from
                    -- Cody & Waite "Software Manual for the Elementary Functions",
                    -- pp125-138. The floating point version is used here.
                    VAL R IS [2.601903036E-6(REAL32), -1.980741872E-4(REAL32),
                              8.333025139E-3(REAL32), -1.666665668E-1(REAL32)] :
                    VAL REAL32 G IS XWork * XWork :
                    RWork := ((((((((R[0]*G)+R[1])*G)+R[2])*G)+R[3])*G)*XWork)+XWork
                --}}}
                --{{{  set sign
                IF
                  (N /\ 1) = 1
                    RWork := - RWork
                  TRUE
                    SKIP
                --}}}
    RESULT RWork
:
