#OPTION "V"  -- no vector space
#COMMENT "Floating point maths library, V1.1 27/03/90"
#COMMENT "(c) Copyright INMOS Limited 1988"
#INCLUDE "mathhdr.inc"
#ifdef C
#PRAGMA TRANSLATE DACOS "DACOS%c"
#else
#ifdef FORTRAN
#PRAGMA TRANSLATE DACOS "DACOS%f"
#PRAGMA TRANSLATE DACOSP "DACOSP%f"
#PRAGMA TRANSLATE DACOSPP "DACOSPP%f"
#endif
#endif

REAL64 FUNCTION DACOS (VAL REAL64 X)

  --{{{  history
  -- V1.0 1-Jul-88
  -- Added OPTION "V"; V1.1 27-Mar-90 SRH
  --}}}

  REAL64 Result :
  VALOF
    IF
      DNOTFINITE (X)
        --{{{  Infs and NaNs
        IF
          DISNAN(X)
            Result := X
          TRUE
            Result := Dundefined.NaN
        --}}}
      --{{{  definitions
      VAL P IS [-6.9674573447350646411E-1(REAL64),
                 1.0152522233806463645E+1(REAL64),
                -3.9688862997504877339E+1(REAL64),
                 5.7208227877891731407E+1(REAL64),
                -2.7368494524164255994E+1(REAL64)] :
      VAL Q IS [-2.3823859153670238830E+1(REAL64),
                 1.5095270841030604719E+2(REAL64),
                -3.8186303361750149284E+2(REAL64),
                 4.1714430248260412556E+2(REAL64),
                -1.6421096714498560795E+2(REAL64)] :
      VAL XWork IS DABS (X) :
      --}}}
      IF
        XWork > DOne
          Result := Dundefined.NaN
        XWork = DOne
          --{{{  result depends on sign
          IF
            X < DZero
              Result := DPi
            TRUE
              Result := DZero
          --}}}
        XWork > DHalf
          --{{{  reduce argument & compute result
          REAL64 Y, G :
          SEQ
            G := DDIVBY2 (DOne - XWork)
            Y := DMULBY2 (DSQRT (G))
            Y := Y + (Y * ((((((((((P[0]*G)+P[1])*G)+P[2])*G)+P[3])*G)+P[4])*G)/
                               (((((((((G+Q[0])*G)+Q[1])*G)+Q[2])*G)+Q[3])*G)+Q[4])))
            IF
              X < DZero
                Result := DPi - Y
              TRUE
                Result := Y
          --}}}
        TRUE
          --{{{  rational approximation
          VAL G IS X * X :
          Result := DPiBy2 - (X + (X *
                    ((((((((((P[0]*G)+P[1])*G)+P[2])*G)+P[3])*G)+P[4])*G)/
                          (((((((((G+Q[0])*G)+Q[1])*G)+Q[2])*G)+Q[3])*G)+Q[4]))))
          --}}}
    RESULT Result
:
#ifdef FORTRAN
--{{{  DACOSP/DACOSPP
-- This 'P' version exists for when the FORTRAN intrinsic DACOS is passed as
-- a parameter to a routine.  The FORTRAN compiler will see the interface to
-- DACOSP.  When DACOSP is called from FORTRAN the default FORTRAN calling
-- conventions are used, i.e. pass a gsb pointer and pass parameters by
-- reference.  Due to these facts DACOSP has a dummy parameter to catch the gsb.
#ifdef FPU
-- On the T8 the REAL64 result is passed back on the floating point
-- stack by occam.  This is the same in FORTRAN and so no special code is
-- required here.  The dummy parameter comes first to take the gsb followed
-- by the REAL64 value. The occam calling convention is to pass REAL64s
-- by reference, so this parameter should match already.

REAL64 FUNCTION DACOSP( VAL INT dummy, VAL REAL64 X ) IS DACOS( X ) :
#else
-- On a non-floating point processor the REAL64 result is passed back by
-- way of a pointer to a result variable by both occam and FORTRAN.  If the
-- routine is written as a function then this result variable is expected
-- by occam to be the first parameter passed onto the stack.  This is not
-- the case in FORTRAN where the first parameter put onto the stack is
-- the gsb, the result parameter follows the gsb.  So the order of the
-- dummy and the result in occam is opposite to that in FORTRAN.   In order
-- to be able to pick up the result pointer we must write the routine as a
-- procedure and cope with the result pointer ourselves.  Therefore, the
-- first parameter is the dummy, the second is the result pointer.  Then
-- comes the REAL64 value.  The occam calling convention is to pass
-- REAL64s by reference, so this parameter should match already.

PROC DACOSPP( VAL INT dummy, REAL64 result, VAL REAL64 X )
  result := DACOS( X )
:
#endif
--}}}
#endif
