#OPTION "V"  -- no vector space
#COMMENT "Floating point maths library, V1.1 27/03/90"
#COMMENT "(c) Copyright INMOS Limited 1988"
#INCLUDE "mathhdr.inc"
#ifdef C
#PRAGMA TRANSLATE ALOG "ALOG%c"
#PRAGMA TRANSLATE ALOG10 "ALOG10%c"
#else
#ifdef FORTRAN
#PRAGMA TRANSLATE ALOG "ALOG%f"
#PRAGMA TRANSLATE ALOGP "ALOGP%f"
#PRAGMA TRANSLATE ALOG10 "ALOG10%f"
#PRAGMA TRANSLATE ALOG10P "ALOG10P%f"
#endif
#endif

--{{{  history
-- V1.0 1-Jul-88
-- Added OPTION "V"; V1.1 27-Mar-90 SRH
--}}}
--{{{  ALOG
REAL32 FUNCTION ALOG (VAL REAL32 X)

  REAL32 RWork :
  VALOF
    IF
      NOTFINITE (X)
        --{{{  NaN or Inf
        IF
          ISNAN (X)
            RWork := X
          TRUE -- must be inf
            RWork := undefined.NaN
        --}}}
      (X <= Zero) -- X is negative
        RWork := undefined.NaN
      TRUE
        REAL32 XWork :
        INT N :
        SEQ
          N, XWork := FLOATING.UNPACK (X)
          XWork := DIVBY2 (XWork) -- to produce fraction in [0.5, 1.0)
          --{{{  Reduced Log
          VAL REAL32 C0 RETYPES #3F3504F3(INT32) : -- SQRT(0.5) in floating point
          VAL A0 IS -5.527074855E-1(REAL32) :
          VAL B0 IS -6.632718214(REAL32) :
          REAL32 Z :
          SEQ
            IF
              XWork > C0
                SEQ
                  Z := ((XWork - Half) - Half) / ((XWork * Half) + Half)
                  N := N + 1             -- to ensure exponent corresponds
              TRUE
                VAL ZNum IS XWork - Half :
                VAL ZDen IS (ZNum * Half) + Half :
                Z := ZNum / ZDen
            VAL W IS Z * Z :
            RWork := Z + (Z * ((W * A0)/(W + B0)))
          --}}}
          IF
            N = 0
              SKIP
            TRUE
              RWork := REAL32 ROUND((REAL64 RWork) + ((REAL64 ROUND N) * DLn2))
    RESULT RWork
:
--}}}
--{{{  ALOG10
REAL32 FUNCTION ALOG10 (VAL REAL32 X)

  REAL32 NLog :
  VALOF
    SEQ
      NLog := ALOG (X)
      IF
        ISNAN (NLog)
          SKIP
        TRUE
          -- LogE = 0.43429449200630187988281250 which is closest IEEE single
          -- precision representation to the exact 0.43429448190325182765...
          VAL REAL32 LogE RETYPES #3EDE5BD9(INT32) :
          NLog := NLog * LogE
    RESULT NLog
:
--}}}
#ifdef FORTRAN
--{{{  ALOGP
REAL32 FUNCTION ALOGP( VAL INT dummy, VAL INT X )

  -- ALOGP exists for when the FORTRAN intrinsic ALOG is passed as a
  -- parameter to a routine.  The FORTRAN compiler will see the interface
  -- to ALOGP.  When ALOGP is called from FORTRAN the default FORTRAN
  -- calling conventions are used, i.e. pass a gsb pointer and pass
  -- parameters by reference.  Due to these facts ALOGP has a dummy
  -- parameter to catch the gsb and ASM code to dereference its argument
  -- which is in fact a pointer to the true argument.
  -- We use INTs as arguments for ALOGP because an INT has the same size
  -- as a pointer.

  REAL32 Xtemp :
  VALOF
    ASM
      LD       X
      LDNL     0
      ST       Xtemp
    RESULT ALOG( Xtemp )
:
--}}}
--{{{  ALOG10P
REAL32 FUNCTION ALOG10P( VAL INT dummy, VAL INT X )

  -- ALOG10P exists for when the FORTRAN intrinsic ALOG10 is passed as a
  -- parameter to a routine.  The FORTRAN compiler will see the interface
  -- to ALOG10P.  When ALOG10P is called from FORTRAN the default FORTRAN
  -- calling conventions are used, i.e. pass a gsb pointer and pass
  -- parameters by reference.  Due to these facts ALOG10P has a dummy
  -- parameter to catch the gsb and ASM code to dereference its argument
  -- which is in fact a pointer to the true argument.
  -- We use INTs as arguments for ALOG10P because an INT has the same size
  -- as a pointer.

  REAL32 Xtemp :
  VALOF
    ASM
      LD       X
      LDNL     0
      ST       Xtemp
    RESULT ALOG10( Xtemp )
:
--}}}
#endif
