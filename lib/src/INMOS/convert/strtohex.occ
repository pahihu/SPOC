#OPTION "E V" -- no compiler libraries, no vector space
#COMMENT "Type conversion library V1.1 22/03/90"
#COMMENT "(c) Copyright INMOS Limited 1988"
PROC STRINGTOHEX (BOOL Error, INT n, VAL []BYTE string)

  --{{{  history
  --  Empty string behaviour corrected 19.2.88
  -- V1.0, 1-Jul-88
  -- Added V to the OPTIONs; V1.1, 22-Mar-90 SRH
  --}}}

  INT chars.scanned:
  INT carry, digit:
  BOOL More :
  --{{{  ValidDigit
  PROC ValidDigit (INT digit, BOOL valid, VAL BYTE ch)
    --convert char to hex digit and set valid flag
    SEQ
      digit := (INT ch)
      IF
        ((INT '0') <= digit) AND (digit <= (INT '9'))
          SEQ
            digit:= (digit - (INT '0'))
            valid := TRUE
        ((INT 'A') <= digit) AND (digit <= (INT 'F'))
          SEQ
            digit:= (digit - ((INT 'A') - 10))
            valid := TRUE
        TRUE
          valid := FALSE
  :
  --}}}
  SEQ
    n := 0
    Error := FALSE
    chars.scanned := 0
    More := (SIZE string) > 0
    digit := 0
    WHILE More AND (chars.scanned < (SIZE string))
      SEQ
        carry, n := SHIFTLEFT (0, n, 4)
        n := n \/ digit
        Error := Error OR (carry <> 0)
        ValidDigit (digit, More, string[chars.scanned])
        chars.scanned := chars.scanned + 1
    IF
      More
        SEQ
          carry, n := SHIFTLEFT (0, n, 4)
          n := n \/ digit
          Error := Error OR (carry <> 0)
      TRUE
        Error := TRUE
:
