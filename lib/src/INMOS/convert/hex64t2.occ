#OPTION "E V" -- no compiler libraries, no vector space
#COMMENT "Type conversion library V1.1 22/03/90"
#COMMENT "(c) Copyright INMOS Limited 1988"
--{{{  history
-- V1.0, 1-Jul-88
-- Added V to the OPTIONs; V1.1, 22-Mar-90 SRH
--}}}
--{{{  STRINGTOHEX64 T2
PROC STRINGTOHEX64 (BOOL Error, INT64 n, VAL []BYTE string)
  --  Empty string behaviour corrected 19.2.88

  [4]INT n RETYPES n:
  INT chars.scanned:
  INT carry, digit, dump:
  BOOL More :
  --{{{  ValidDigit
  PROC ValidDigit (INT digit, BOOL valid, VAL BYTE ch)
    --convert char to hex digit and set valid flag
    SEQ
      digit := (INT ch)
      IF
        ((INT '0') <= digit) AND (digit <= (INT '9'))
          SEQ
            digit:= (digit - (INT '0'))
            valid := TRUE
        ((INT 'A') <= digit) AND (digit <= (INT 'F'))
          SEQ
            digit:= (digit - ((INT 'A') - 10))
            valid := TRUE
        TRUE
          valid := FALSE
  :
  --}}}
  SEQ
    n := [0, 0, 0, 0]
    Error := FALSE
    chars.scanned := 0
    More := (SIZE string) > 0
    digit := 0
    WHILE More AND (chars.scanned < (SIZE string))
      SEQ
        --{{{  n := n << 4 for functions compiler
        carry, dump := SHIFTLEFT (0, n[3], 4)
        n[3], dump  := SHIFTLEFT (n[3], n[2], 4)
        n[2], dump  := SHIFTLEFT (n[2], n[1], 4)
        n[1], n[0]  := SHIFTLEFT (n[1], n[0], 4)
        --}}}
        n[0] := n[0] \/ digit
        Error := Error OR (carry <> 0)
        ValidDigit (digit, More, string [chars.scanned])
        chars.scanned := chars.scanned + 1
    IF
      More
        SEQ
          --{{{  n := n << 4 for functions compiler
          carry, dump := SHIFTLEFT (0, n[3], 4)
          n[3], dump  := SHIFTLEFT (n[3], n[2], 4)
          n[2], dump  := SHIFTLEFT (n[2], n[1], 4)
          n[1], n[0]  := SHIFTLEFT (n[1], n[0], 4)
          --}}}
          n[0] := n[0] \/ digit
          Error := Error OR (carry <> 0)
      TRUE
        Error := TRUE
:
--}}}
--{{{  HEX64TOSTRING T2
#USE "ioconv.lib"
PROC HEX64TOSTRING (INT len, []BYTE string, VAL INT64 n)

  VAL [4]INT n RETYPES n:
  SEQ
    HEXTOSTRING (len, string, n[3])
    HEXTOSTRING (len, [string FROM 4 FOR 4], n[2])
    HEXTOSTRING (len, [string FROM 8 FOR 4], n[1])
    HEXTOSTRING (len, [string FROM 12 FOR 4], n[0])
    len := 16
:
--}}}
