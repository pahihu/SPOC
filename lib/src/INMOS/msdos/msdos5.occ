#OPTION "E"   -- no compiler libraries
#COMMENT "MSDOS library V1.0"
#COMMENT "(c) Copyright INMOS Limited 1988"
#INCLUDE "hostio.inc"
#INCLUDE "msdos.inc"

PROC dos.port.read (CHAN OF SP fs, ts, VAL INT16 port.location,
                    BYTE value, BYTE result)

  --{{{  specification
  -- Purpose:  To read a byte from a given port.
  -- Channels: fs - from server
  --           ts - to server
  -- In:       port.location - the address where the port is to be found,
  --           the address being in the I/O space of the PC and hence is
  --           an unsigned number between 0 and 64K
  -- Out:      value - the value read from port.location
  -- Out:      result - spr.ok if the read was successful; otherwise
  --           >= spr.operation.failed
  -- Notes:    No check is made to ensure that the value received from
  --           the port ( if any ) is valid.
  --           The value returned in value is that of the given address
  --           at the moment the port is read by the host file server.
  --}}}

  [sp.min.packet.data.size]BYTE buffer :
  INT16 temp.INT16 :
  SEQ
    --{{{  pack up the data into the buffer (4 bytes)
    buffer [0] := sp.DOS.tag
    buffer [1] := dos.port.read.tag
    
    temp.INT16 := INT16 port.location
    VAL [2]BYTE b.temp RETYPES temp.INT16 :
    [buffer FROM 2 FOR 2] := b.temp
    --}}}
    --{{{  send and receive the buffer
    ts ! INT16 sp.min.packet.data.size :: buffer
    fs ? temp.INT16 :: buffer
    --}}}
    --{{{  unpack the data                  (2 bytes)
    result := buffer [0]
    value  := buffer [1]
    --}}}
:
