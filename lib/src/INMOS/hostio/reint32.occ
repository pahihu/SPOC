#COMMENT "Host i/o library, V1.0, 25/05/90"
#COMMENT "(c) Copyright INMOS Limited 1990"
#INCLUDE "hostio.inc"
#USE "solib.lib"
#USE "convert.lib"

PROC so.read.echo.int32 (CHAN OF SP fs, ts, INT32 n, BOOL error)

  --{{{  specification
  -- Purpose:  To read a string containing a representation of a 32-bit
  --           decimal integer from the keyboard, and to echo it to standard
  --           out.
  -- Channels: fs - from server
  --           ts - to server
  -- Out:      n - if error is FALSE, the value of the integer read; undefined
  --           otherwise
  -- Out:      error - FALSE if nothing went wrong in the operation; otherwise
  --           TRUE:
  --           1) if the integer read overflows the INT32 range
  --           2) if a non-numeric character is found in the input string ( other
  --           than a + or - in the first character position )
  -- Notes:    The integer may commence with a + or a - sign.
  --           The number typed at the keyboard must be terminated by pressing
  --           'RETURN'.
  --}}}

  [11]BYTE string : -- 11 digits in -2147483648 = MOSTNEG INT32
  INT len :
  BYTE result :

  SEQ
    so.read.echo.line (fs, ts, len, string, result)
    IF
      result = spr.ok
        STRINGTOINT32 (error, n, [string FROM 0 FOR len])
      TRUE
        error := TRUE
:
