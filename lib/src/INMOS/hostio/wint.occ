#COMMENT "Host i/o library, V1.1, 25/05/90"
#COMMENT "(c) Copyright INMOS Limited 1989"
#INCLUDE "hostio.inc"
#USE "splib.lib"
#USE "convert.lib"
--{{{  so.fwrite.int
PROC so.fwrite.int (CHAN OF SP fs, ts, VAL INT32 streamid, VAL INT n, width,
                    BYTE result)

  --{{{  specification
  -- Purpose:  To write to the specified file stream an integer as decimal
  --           ASCII digits, padded out with leading spaces and an optional
  --           sign to the specified field width.
  -- Channels: fs - from server
  --           ts - to server
  -- In:       streamid - the identification number of the stream to which
  --           the string representing the integer is to be sent
  -- In:       n - the integer that is desired to be written
  -- In:       width - the desired field width of the string
  -- Out:      result - equal to spr.ok if the string was written all right;
  --           otherwise not all of the string could be written, in which
  --           case result takes on a value of spr.notok
  -- Notes:    If the field width is too small for the number, then it is
  --           widened as necessary; a zero value for the field width will
  --           give minimum width; a negative value is an error.
  --}}}

  IF
    width < 0
      STOP
    TRUE
      [11]BYTE string : -- 11 characters in -2147483648 = MOSTNEG INT32;
      INT len, len.done :
      SEQ
        INTTOSTRING (len, string, n)
        --{{{  output any necessary spaces
        VAL spaces IS "                    " :  -- 20 spaces
        INT number.of.spaces, fill.this.time :
        SEQ
          number.of.spaces := width - len
          WHILE number.of.spaces > 0
            SEQ
              IF
                number.of.spaces > (SIZE spaces)
                  fill.this.time := SIZE spaces
                TRUE
                  fill.this.time := number.of.spaces
        
              sp.write (fs, ts, streamid, [spaces FROM 0 FOR fill.this.time],
                        len.done, result)
              number.of.spaces := number.of.spaces - fill.this.time
        --}}}output any necessary spaces
        sp.write (fs, ts, streamid, [string FROM 0 FOR len], len.done, result)
        --{{{  check operation was ok
        IF
          (result = spr.ok) AND (len.done <> len)
            result := spr.notok
          TRUE
            SKIP
        --}}}check operation was ok
:
--}}}so.fwrite.int
--{{{  so.write.int
PROC so.write.int (CHAN OF SP fs, ts, VAL INT n, width)

  --{{{  specification
  -- Purpose:  To write to standard out an integer as decimal
  --           ASCII digits, padded out with leading spaces and an
  --           optional sign to the specified field width.
  -- Channels: fs - from server
  --           ts - to server
  -- In:       n - the integer that is desired to be written
  -- In:       width - the desired field width of the string
  -- Notes:    If the field width is too small for the number, then it is
  --           widened as necessary; a zero value for the field width will
  --           give minimum width; a negative field width is an error.
  --}}}

  BYTE result :
  so.fwrite.int (fs, ts, spid.stdout, n, width, result)
:
--}}}so.write.int
