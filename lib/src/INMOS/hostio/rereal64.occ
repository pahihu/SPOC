#COMMENT "Host i/o library, V1.1, 11/06/90"
#COMMENT "(c) Copyright INMOS Limited 1989"
#INCLUDE "hostio.inc"
#USE "solib.lib"
#USE "convert.lib"

PROC so.read.echo.real64 (CHAN OF SP fs, ts, REAL64 n, BOOL error)

  --{{{  specification
  -- Purpose:  To read a string containing a representation of a double
  --           precision real in occam syntax ( though a + or - is allowed
  --           as first character ) from the keyboard and to echo it to
  --           standard out.
  -- Channels: fs - from server
  --           ts - to server
  -- Out:      n - if error is FALSE, the IEEE double precision format of the
  --           real that was read, otherwise undefined
  -- Out:      error - FALSE if the real was read and converted to binary
  --           without problem; TRUE otherwise - if the string read does not
  --           conform to the syntax of an occam real number, other than
  --           the fact that a plus or minus sign is allowed as the first
  --           character
  -- Notes:    The number typed at the keyboard must be terminated by pressing
  --           'RETURN'.
  --}}}
  --{{{  history
  -- Increased the size of string from 24 to 30 so that it is the
  -- same size as in so.fwrite.real64; new version is 1.1. 11-June-90 SRH
  --}}}

  [30]BYTE string :
  INT len :
  BYTE result :

  SEQ
    so.read.echo.line (fs, ts, len, string, result)
    IF
      result = spr.ok
        STRINGTOREAL64 (error, n, [string FROM 0 FOR len])
      TRUE
        error := TRUE
:
